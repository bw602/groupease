plugins {
  id "com.moowork.node" version "1.2.0"
}

node {

  /* Version of node to use. */
  version = '9.3.0'

  /* Version of npm to use. */
  npmVersion = '5.6.0'

  /* Base URL for fetching node distributions (change if you have a mirror). */
  distBaseUrl = 'https://nodejs.org/dist'

  /* If true, it will download node using above parameters. */
  download = true

  /* Set the work directory for unpacking node. */
  workDir = file("${project.buildDir}/nodejs")

  /* Set the work directory for NPM. */
  npmWorkDir = file("${project.buildDir}/npm")

  /* Set the work directory where node_modules should be located. */
  nodeModulesDir = file("${project.projectDir}")

}

/* Run "npm run build" which executes the "build" script in package.json. */
task ngBuild(type: NpmTask) {
  args = ['run', 'build']
}
build.dependsOn(ngBuild)
ngBuild.dependsOn(check)

/* Run "npm run lint" which executes the "lint" script in package.json. */
task ngLint(type: NpmTask) {
  args = ['run', 'lint']
}
check.dependsOn(ngLint)

/* Run "npm run test" which executes the "test" script in package.json. */
task ngTest(type: NpmTask) {
  args = ['run', 'test']
}
test.dependsOn(ngTest)

/*
 * Pack as webjar.
 * Don't use normal "META-INF/resources/webjars/${baseName}/${project.version}"
 * path since hosting page is in this jar.
 */
jar {
  dependsOn 'ngBuild'
  from 'build/dist'
  into "META-INF/resources"
}
